# Contributor: Oleksiy Zakharov <alexzkhr@gmail.com>

pkgname=tess_svg
pkgver=0.2.0
pkgrel=18
pkgdesc="Translates SVG file into vertexes collections."

provides=('tess_svg')

#Not sure, this one may work on arm too ...but never tried
arch=('i686' 'x86_64' 'arm')
url="https://github.com/alexzk1/tess_svg"

license=('GPL')
depends=(boost-libs glu)
makedepends=(cmake boost)
source=(git+$url.git)
md5sums=('SKIP')

build() {
  cd $srcdir/tess_svg
  cmake -S . -B build -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build --verbose
}

check() {
  cd $srcdir/tess_svg/build
  # Run all tests from CMake added as add_test()
  ctest --output-on-failure
}

package() {
  cd $srcdir/tess_svg/build
  install -D -m755 tess_svg $pkgdir/usr/bin/tess_svg
  install -d $pkgdir/usr/share/tess_svg
  cp $srcdir/tess_svg/generated_support/*.* $pkgdir/usr/share/tess_svg
}
